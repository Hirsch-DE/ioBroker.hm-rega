!# rooms.fn 1.2
!#
!# Dieses Script gibt eine Liste der Raeume als JSON Objekt aus
!#
!# 5'2013-7'2014 bluefox, hobbyquaker
!#

object  oRoom;
string  sRoomId;
string  sChannelId;
object  oChannel;
boolean bFirst       = true;
boolean bFirstSecond = true;

Write("{");
foreach (sRoomId, dom.GetObject(ID_ROOMS).EnumUsedIDs())
{
    if (bFirst == false) {
      WriteLine(',');
    } else {
      bFirst = false;
    }
    oRoom = dom.GetObject(sRoomId);
	Write('"' # sRoomId # '": ');
    Write('{"Name": "');
    WriteURL(oRoom.Name())
    Write('", "TypeName":"' # oRoom.TypeName() # '_ROOMS');
    Write('", "EnumInfo":"');
    WriteURL(oRoom.EnumInfo());
    Write('", "Channels":[');
	bFirstSecond = true;
    foreach(sChannelId, oRoom.EnumIDs()) {
        oChannel = dom.GetObject(sChannelId);
        ! Objekt ueberspringen, falls nicht vom Typ CHANNEL (33)
        if (oChannel.Type() != 33) { continue; }
        
		if (bFirstSecond == false) {
		  Write(',');
		} else {
		  bFirstSecond = false;
		}
        string sIfaceId = oChannel.Interface();
        string sIface = dom.GetObject(sIfaceId).Name();
        Write('{"Address":"' # oChannel.Address() # '",');
        Write('"Interface":"' # sIface # '"}');

    }
    Write(']}');
}
Write('}');
